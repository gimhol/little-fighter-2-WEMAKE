uniform sampler2D tex;
/** 一倍纹理图的宽度（像素）*/
uniform float tw;
/** 一倍纹理图的高度（像素）*/
uniform float th;
/** 当前纹理图倍数 */
uniform float tsw;
uniform float tsh;

uniform float repeatX;
uniform float repeatY;
uniform float offsetX;
uniform float offsetY;

/** 一倍纹理图下，纹理图中截取坐标X（像素）*/
uniform float x;
/** 一倍纹理图下，纹理图中截取坐标Y（像素）*/
uniform float y;
/** 一倍纹理图下，纹理图中截取宽度（像素）*/
uniform float w;
/** 一倍纹理图下，纹理图中截取高度（像素）*/
uniform float h;
/** 描边宽度（像素）*/
uniform float outlineWidth;
/** 描边透明度（0~1）*/
uniform float outlineAlpha;
/** 描边颜色 */
uniform vec3 outlineColor;
/** 灰度 */
uniform float gray;
/** 混色强度 */
uniform float mixStength;
/** 混色 */
uniform vec3 mixColor;
/** opacity */
uniform float opacity;
uniform bool keepout;

uniform bool cover;
/** 色强度 */
uniform float coverStength;
/** 色 */
uniform vec3 coverColor;

/** 背景色 */
uniform vec3 bgColor;
/** 背景色透明度 */
uniform float bgAlpha;

/** 前景色 */
uniform vec3 fgColor;
/** 前景色透明度 */
uniform float fgAlpha;

// 灰度权重
const vec3 GRAY_WEIGHT = vec3(0.299, 0.587, 0.114);

const float gamma = 2.2;

varying vec2 vUv;

vec3 gamma_correct(vec3 color) {
  return pow(color, vec3(1.0 / gamma));
}

vec3 gamma_invert(vec3 color) {
  return pow(color, vec3(gamma));
}

vec3 to_gray(vec3 color, float strength) {
  float gray = dot(color, GRAY_WEIGHT);
  return mix(color, vec3(gray), strength);
}

vec4 bgfg(vec3 c0, float a0, vec3 c1, float a1) {
  if(a0 <= 0.0)
    return vec4(c1, a1);
  if(a1 <= 0.0)
    return vec4(c0, a0);
  vec4 ret = vec4(0, 0, 0, 0);
  ret.a = a0 * (1.0 - a1) + a1;
  ret.rgb = (c0 * a0 * (1.0 - a1) + c1 * a1) / ret.a;
  return ret;
}

void apply(vec4 color) {
  if(cover) {
    color.rgb = gamma_correct(coverColor);
    color.a = coverStength;
  }
  if(gray > 0.0)
    color.rgb = to_gray(color.rgb, gray);
  if(mixStength > 0.0)
    color.rgb = mix(color.rgb, gamma_correct(mixColor), mixStength);
  if(bgAlpha > 0.0)
    color = bgfg(bgColor, bgAlpha, color.rgb, color.a);
  if(fgAlpha > 0.0)
    color = bgfg(color.rgb, color.a, fgColor, fgAlpha);
  color.a *= opacity;
  gl_FragColor = color;
}

void main() {
  /** 原图像素宽 */
  float ow = tw / tsw;
  /** 原图像素高 */
  float oh = th / tsh;

  float uv_x = vUv.x * w / ow + x / ow;
  float uv_y = 1.0 + vUv.y * h / oh - (y + h) / oh;
  vec2 uv = vec2(uv_x, uv_y);

  if(!keepout && (uv.x < 0.0 || uv.x > 1.0 || uv.y < 0.0 || uv.y > 1.0)) {
    // 超出纹理图的部分将不显示
    discard;
  }
  vec4 color = texture2D(tex, uv);
  color.rgb = gamma_correct(color.rgb);

  /* 无需描边时，仅处理颜色 */
  if(outlineAlpha <= 0.0 || outlineWidth <= 0.0) {
    apply(color);
    return;
  }

  /* 检查中心与四周颜色 */
  float outline = 0.0;
  vec2 texel = vec2(outlineWidth) / vec2(textureSize(tex, 0));
  float center = texture2D(tex, uv).a;

  vec2 coord_up = uv + vec2(0, -texel.y);
  vec2 coord_down = uv + vec2(0, texel.y);
  vec2 coord_left = uv + vec2(-texel.x, 0);
  vec2 coord_right = uv + vec2(texel.x, 0);
  float up = texture2D(tex, coord_up).a;
  float down = texture2D(tex, coord_down).a;
  float left = texture2D(tex, coord_left).a;
  float right = texture2D(tex, coord_right).a;
  if(coord_up.y < 1.0 - (y + h) / oh)
    up = 0.0;
  if(coord_down.y > y / oh)
    down = 0.0;
  if(coord_left.x < x / ow)
    left = 0.0;
  if(coord_right.x > (x + w) / ow)
    right = 0.0;

  outline = max(max(abs(center - up), abs(center - down)), max(abs(center - left), abs(center - right)));
  if(outline <= 0.1 || center >= 0.5) {
    apply(color);
    return;
  }

  // 描边
  color = vec4(gamma_correct(outlineColor), outlineAlpha);
  if(bgAlpha > 0.0)
    color = bgfg(bgColor, bgAlpha, color.rgb, color.a);
  if(fgAlpha > 0.0)
    color = bgfg(color.rgb, color.a, fgColor, fgAlpha);
  color.a *= opacity;
  gl_FragColor = color;

}